; 32-bit Fixed-Point (FP) Q16.16 Routines
;

; Push 32-bit FP in r0(integer):r1(mantissa) to top of stack (SIH,SIL:SFH,SFL)
;
FPPUSH:
		pha
		phx
		ldx FPSP
		lda r0L
		sta SIL,x
		lda r0H
		sta SIH,x
		lda r1L
		sta SFL,x
		lda r1H
		sta SFH,x
		inx		; FPSP += 1
		stx FPSP	; update FPSP
		plx
		pla
		rts

; Pull 32-bit FP from top of stack (SIH,SIL:SFH,SFL) to r0(integer):r1(mantissa)
;
FPPULL:
		pha
		phx
		ldx FPSP	; load stack pointer
		dex		; point to topmost element
		lda SIL,x
		sta r0L
		lda SIH,x
		sta r0H
		lda SFL,x
		sta r1L
		lda SFH,x
		sta r1H
		stx FPSP	; update FPSP to new empty location
		plx
		pla
		rts

; Copies 32-bit FP from top of stack (SIH,SIL:SFH,SFL) to r0(integer):r1(mantissa)
;
FPPEEK:
		pha
		phx
		ldx FPSP	; load stack pointer
		lda SIL-1,x
		sta r0L
		lda SIH-1,x
		sta r0H
		lda SFL-1,x
		sta r1L
		lda SFH-1,x
		sta r1H
		plx
		pla
		rts

; Duplicate top of stack
; ( a -- a a )
;
FPDUP:
		pha
		phx
		ldx FPSP
		lda SIL-1,x	; copy SIL below
		sta SIL,x	; ... to current
		lda SIH-1,x	; copy SIH below
		sta SIH,x	; ... to current
		lda SFL-1,x	; copy SFL below
		sta SFL,x	; ... to current
		lda SFH-1,x	; copy SFH below
		sta SFH,x	; ... to current
		inx		; point to next empty location
		stx FPSP
		plx
		pla
		rts

; Drop the top of stack
; ( a -- )
;
FPDROP:
		dec FPSP	; point to TOP "a" element, now empty
		rts

; Swap top two elements
; ( a b -- b a )
;
FPSWAP:
		pha
		phx
		ldx FPSP
		lda SIL-2,x
		sta FPTEMP
		lda SIH-2,x
		sta FPTEMP+1
		lda SFL-2,x
		sta FPTEMP+2
		lda SFH-2,x
		sta FPTEMP+3
		lda SIL-1,x
		sta SIL-2,x
		lda SIH-1,x
		sta SIH-2,x
		lda SFL-1,x
		sta SFL-2,x
		lda SFH-1,x
		sta SFH-2,x
		lda FPTEMP
		sta SIL-1,x
		lda FPTEMP+1
		sta SIH-1,x
		lda FPTEMP+2
		sta SFL-1,x
		lda FPTEMP+3
		sta SFH-1,x
		plx
		pla
		rts
